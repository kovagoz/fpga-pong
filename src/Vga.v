/*

  Signals generated by this module:

  | 639 | 640 | 641        | 799 | 800 |  1    HCNTR
  /""\__/""\__/""\__ ... __/""\__/""\__/""\__  CLK
  __________________________________/"""""\__  HRESET
  _________/""""""""""""""""""""""""\________  HBLANK
                   480                 | 481   VCNTR
  __________________________________/""""""""  VBLANK
                   525                 |  1    VCNTR
  """"""""""""""""""""""""""""""""""\________  VBLANK
  __________________________________/""""""""  VRESET
                    1                  |  2    VCNTR
  """"""""""""""""""""""""""""""""""\________  VRESET

*/

`include "Vga_Timing.v"

// Tell if the given (x, y) coordinate is in the visible area of
// the sceeen or not.
`define IS_VISIBLE(x, y) x <= `H_VISIBLE_AREA && y <= `V_VISIBLE_AREA

module Vga(
  input  i_Clk,
  input  i_Video,

  output o_HSync,
  output o_VSync,

  output o_HBlank,
  output o_VBlank,

  output o_HReset,
  output o_VReset,

  output [9:0] o_HC, // Horizontal counter
  output [9:0] o_VC, // Vertical counter

  output o_Video);

  // Initial position of the H/V counters (useful for testing).
  parameter p_HPOS = 1;
  parameter p_VPOS = 1;

  // X/Y position of the "electron beam".
  reg [9:0] x = p_HPOS;
  reg [9:0] y = p_VPOS;

  // Levels of the RESET signals.
  reg hr = 0;
  reg vr = 0;

  // Levels of the BLANK signals.
  reg hb = 0;
  reg vb = 0;

  // Horizontal counter from 1 to H_MAX.
  always @(posedge i_Clk) begin
    if (x == `H_MAX) begin
      x <= 1;

      // Vertical counter from 1 to V_MAX.
      if (y == `V_MAX)
        y <= 1;
      else
        y <= y + 1;
    end else begin
      x <= x + 1;
    end
  end

  // Shape the horizontal and vertical RESET and BLANK signals.
  always @(negedge i_Clk) begin
    hr <= x == `H_MAX;
    hb <= x >= `H_VISIBLE_AREA && x < `H_MAX;
    vr <= (y == 1 && x < `H_MAX) || (y == `V_MAX && x== `H_MAX);
    vb <= (y > `V_VISIBLE_AREA || y == `V_VISIBLE_AREA && x == `H_MAX)
       && (y < `V_MAX || y == `V_MAX && x < `H_MAX);
  end

  // Shape the horizontal and vertical sync pulses.
  assign o_HSync = (x < `H_PULSE_HEAD) || (x > `H_PULSE_TAIL);
  assign o_VSync = (y < `V_PULSE_HEAD) || (y > `V_PULSE_TAIL);

  assign o_HBlank = hb;
  assign o_VBlank = vb;

  assign o_HReset = hr;
  assign o_VReset = vr;

  // Enable video signal only in the visible area of the screen.
  assign o_Video = (`IS_VISIBLE(x, y) && i_Video);

  assign o_HC = x;
  assign o_VC = y;

endmodule
